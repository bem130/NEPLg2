#indent 4

#import "std/mem"
#use std::mem::*

// Generic Option type
enum Option<.T>:
    None
    Some <.T>

fn none <.T> <()->Option<.T>> ():
    Option<.T>::None

fn some <.T> <(.T)->Option<.T>> (v):
    Option<.T>::Some v

fn is_some <.T> <(Option<.T>)->bool> (o):
    match o:
        Some v:
            true
        None:
            false

fn is_none <.T> <(Option<.T>)->bool> (o):
    match o:
        Some v:
            false
        None:
            true

fn unwrap <.T> <(Option<.T>)->.T> (o):
    match o:
        Some v:
            v
        None:
            // TODO: panic intrinsic
            #intrinsic "unreachable" <> ()

fn unwrap_or <.T> <(Option<.T>,.T)->.T> (o, default):
    match o:
        Some v:
            v
        None:
            default

fn map <.T,.U> <(Option<.T>, (.T)->.U)->Option<.U>> (o, f):
    match o:
        Some v:
            some<.U> f v
        None:
            none<.U>
